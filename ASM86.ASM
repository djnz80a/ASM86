
	ORG	100H

R.BUF	EQU	1000H	;buffer for read
W.BUF	EQU	800H	;buffer for write

W.LBL	EQU	6000H	;label

	LD	C,2CH
	CALL	0005H

	PUSH	DE
	PUSH	HL
	LD	L,H
	LD	H,0
	CALL	PUTDC
	LD	A,':'
	CALL	DOS02
	POP	HL
	LD	H,0
	CALL	PUTDC
	LD	A,':'
	CALL	DOS02

	POP	HL
	PUSH	HL
	LD	L,H
	LD	H,0
	CALL	PUTDC
	LD	A,':'
	CALL	DOS02
	POP	HL
	LD	H,0
	CALL	PUTDC

	CALL	CRLF

	LD	HL,005CH
	LD	DE,FILE1
	LD	BC,12
	LDIR

	XOR	A
	LD	(FLG.1),A
	LD	(FLG.2),A
	LD	(FLG.3),A

	LD	C,0FFH
	LD	HL,006CH
	LD	DE,006CH+1
	LD	B,11
I1:	LD	A,(DE)
	CP	20H
	JR	NZ,I2
	INC	DE
	DJNZ	I1
	LD	C,00H
	LD	HL,005CH
I2:	LD	A,C
	LD	(FLG.1),A
	LD	DE,FILE2
	LD	BC,12
	LDIR

	LD	A,(FLG.1)
	OR	A
	JR	NZ,I3

	LD	HL,FILE2+9
	LD	(HL),'C'
	INC	HL
	LD	(HL),'O'
	INC	HL
	LD	(HL),'M'
I3:
	LD	DE,MES1
	CALL	DOS09

	XOR	A
	LD	(F.TYPE),A	;0=com,1=obj

	LD	HL,0080H
	LD	C,(HL)
	LD	B,H
	INC	HL
	LD	D,H
	LD	E,L
	ADD	HL,BC
	LD	(HL),00H

SW:	LD	A,(DE)
	OR	A
	JR	Z,SW90
	INC	DE
	CP	'/'
	JR	NZ,SW

	LD	A,(DE)
	CALL	UPPER
	CP	'B'
	JR	NZ,SW10
	LD	A,01H
	LD	(F.TYPE),A
	LD	A,(FLG.1)
	OR	A
	JR	NZ,SW50
	LD	HL,FILE2+9
	LD	(HL),'O'
	INC	HL
	LD	(HL),'B'
	INC	HL
	LD	(HL),'J'
	JR	SW50
SW10:
	CP	'L'
	JR	NZ,SW12
	LD	A,0FFH
	LD	(FLG.3),A
	JR	SW50
SW12:
	CP	'R'
	JR	NZ,SW13
	LD	A,0FFH
	LD	(FLG.R),A
	JR	SW50

SW13:
	CP	'3'
	JR	NZ,SW14
	LD	A,0FFH
	LD	(FLG.386),A
	JR	SW50

SW14:

SW50:
	INC	DE
	JR	SW

SW90:
	LD	DE,USRDMA
	CALL	DOS1A

	LD	DE,FILE1
	CALL	DOS0F
	JP	NZ,ERR0	;read file open error

	LD	DE,FILE2
	CALL	DOS16
	OR	A
	JP	NZ,ERR2

	LD	A,0FFH
	LD	(FLAG_FILE2),A

	LD	A,(F.TYPE)
	LD	HL,0
	OR	A
	JR	Z,I5
	LD	L,7
I5:	LD	(FILE2+33),HL
	LD	L,0
	LD	(FILE2+35),HL
	LD	HL,1
	LD	(FILE2+14),HL
	LD	HL,W.BUF
	LD	(W.LEFT),HL
	LD	HL,USRDMA+R.BUF
	LD	(W.PNT),HL

	LD	HL,LBL.PNT.TOP
	LD	BC,200H+200H
INIT50:	LD	(HL),00H
	INC	HL
	DEC	BC
	LD	A,B
	OR	C
	JR	NZ,INIT50

MAIN:	LD	A,1
	LD	(PASS),A
	CALL	ASM

	LD	A,2
	LD	(PASS),A
	CALL	ASM

	LD	DE,USRDMA+R.BUF
	CALL	DOS1A

	LD	HL,W.BUF
	LD	DE,(W.LEFT)
	OR	A
	SBC	HL,DE
	JR	Z,I10
	LD	DE,FILE2
	CALL	DOS26
	JP	NZ,ERR3
I10:
	LD	A,(F.TYPE)
	OR	A
	JR	Z,I12

	LD	A,0FEH
	LD	(WORK2),A

	LD	HL,(ORG.ADR)
	LD	(WORK2+1),HL	;start
	LD	(WORK2+5),HL	;excute

	LD	DE,(O.BYTE)
	ADD	HL,DE
	DEC	HL
	LD	(WORK2+3),HL	;end

	LD	DE,WORK2
	CALL	DOS1A

	LD	HL,0
	LD	(FILE2+33),HL
	LD	(FILE2+35),HL
	INC	L
	LD	(FILE2+14),HL
	LD	DE,FILE2
	LD	HL,7
	CALL	DOS26

I12:	LD	DE,FILE2
	CALL	DOS10
	JP	NZ,ERR3

	XOR	A
	LD	(FLAG_FILE2),A

	LD	DE,MES10	;end address =
	CALL	DOS09

	LD	HL,(O.ADR1)
	DEC	HL
	LD	A,H
	CALL	PUTH1
	LD	A,L
	CALL	PUTH1

	CALL	CRLF

	LD	DE,MES11
	CALL	DOS09

	LD	HL,(LBL.NO)
	CALL	PUTDC
;	LD	A,H
;	CALL	PUTH1
;	LD	A,L
;	CALL	PUTH1

	CALL	CRLF

	LD	A,(FLG.3)
	OR	A
;	RET	Z
	JP	Z,EXIT

	LD	HL,FILE2
	LD	B,25H
PUT.L0:	LD	(HL),00H
	INC	HL
	DJNZ	PUT.L0

	LD	HL,FILE1
	LD	DE,FILE2
	LD	BC,12
	LDIR

	LD	HL,FILE2+9
	LD	(HL),'L'
	INC	HL
	LD	(HL),'B'
	INC	HL
	LD	(HL),'L'

	LD	DE,USRDMA
	CALL	DOS1A

	LD	DE,FILE2
	CALL	DOS16
	OR	A
	JP	NZ,ERR2

	LD	A,0FFH
	LD	(FLAG_FILE2),A

	LD	HL,0000H
	LD	(FILE2+33),HL
	LD	(FILE2+35),HL
	INC	L
	LD	(FILE2+14),HL

	LD	HL,W.BUF
	LD	(W.LEFT),HL
	LD	HL,USRDMA+R.BUF
	LD	(W.PNT),HL

	LD	DE,(LBL.NO)
	LD	A,D
	OR	E
	RET	Z
	LD	HL,W.LBL
PUT.L:	LD	B,(HL)
	INC	HL
	INC	HL
	INC	HL
PUT.L1:	LD	A,(HL)
	CALL	PUT.L20;CALL	DOS02
	INC	HL
	DJNZ	PUT.L1
	LD	A,09H
	CALL	PUT.L20;DOS02
	LD	A,(HL)
	PUSH	AF
	INC	HL
	LD	A,(HL)
	CALL	PUT.L30
	POP	AF
	CALL	PUT.L30
	LD	A,0DH
	CALL	PUT.L20
	LD	A,0AH
	CALL	PUT.L20
	INC	HL
	DEC	DE
	LD	A,D
	OR	E
	JR	NZ,PUT.L

	LD	A,1AH
	CALL	PUT.L20

	LD	HL,W.BUF
	LD	DE,(W.LEFT)
	OR	A
	SBC	HL,DE
	JR	Z,PUT.L10

	PUSH	HL
	LD	DE,USRDMA+R.BUF
	CALL	DOS1A
	POP	HL
	LD	DE,FILE2
	CALL	DOS26
	JP	NZ,ERR3

PUT.L10:
	LD	DE,FILE2
	CALL	DOS10
	JP	NZ,ERR3

	XOR	A
	LD	(FLAG_FILE2),A
EXIT:
	LD	C,2CH
	CALL	0005H

	PUSH	DE
	PUSH	HL
	LD	L,H
	LD	H,0
	CALL	PUTDC
	LD	A,':'
	CALL	DOS02
	POP	HL
	LD	H,0
	CALL	PUTDC
	LD	A,':'
	CALL	DOS02

	POP	HL
	PUSH	HL
	LD	L,H
	LD	H,0
	CALL	PUTDC
	LD	A,':'
	CALL	DOS02
	POP	HL
	LD	H,0
	CALL	PUTDC

	CALL	CRLF

	RET

PUT.L20:
	PUSH	HL
	PUSH	DE
	PUSH	BC
	CALL	PUTCHR
	POP	BC
	POP	DE
	POP	HL
	RET

PUT.L30:
	PUSH	AF
	RRCA
	RRCA
	RRCA
	RRCA
	CALL	PUT.L35
	POP	AF
PUT.L35:
	AND	0FH
	CP	10
	JR	C,$+4
	ADD	A,7
	ADD	A,'0'
	CALL	PUT.L20
	RET
;
;
MES10:	DEFM	'end address = $'

MES11:	DEFM	'label(s)    = $'
;
;
;
ASM:
	XOR	A
	LD	(LOAD.F),A
	LD	HL,0
	LD	(FILE1+33),HL
	LD	(FILE1+35),HL
	INC	L
	LD	(FILE1+14),HL
	LD	HL,FILE1+25H+2
	LD	(FCB),HL
	LD	HL,FILE1
	LD	DE,FILE1+25H+2
	LD	BC,25H
	LDIR

PNO:	LD	DE,MES2
	CALL	DOS09
	LD	A,(PASS)
	ADD	A,'0'
	CALL	DOS02
	CALL	CRLF

	LD	HL,0000H	;real address
	LD	(O.ADR1),HL
	LD	HL,0000H	;offset
	LD	(O.ADR2),HL
	LD	(O.BYTE),HL	;byte

	LD	HL,1
	LD	(FILE1+25H),HL
	LD	(LIN.NO),HL

	LD	HL,W.LBL
	LD	(LBL.NX),HL
	LD	A,(PASS)
	DEC	A
	JR	NZ,PNO2
	LD	HL,0
	LD	(LBL.NO),HL
	LD	(ERR.NO),HL
PNO2:
	LD	HL,0
	LD	(S.LEFT),HL
	XOR	A
	LD	(FL.END),A



MAIN01:	LD	(STACK),SP
	CALL	GETLIN
	LD	A,(L.DATA)
	CP	1AH
	JP	NZ,M10
MAIN02:	CALL	FCBDEC
	RET	C
	JR	MAIN01
;	LD	A,(PASS)
;	CP	2
;	JR	Z,M2
;	LD	A,(FLG.1)
;	OR	A
;	JR	Z,M10
;	CALL	PUTLIN
;	LD	A,09H
;	CALL	DOS02
;	CALL	DOS02
;	JR	M10
;M2:	LD	A,(FLG.2)
;	OR	A
;	JR	Z,M10
;	CALL	PUTLIN
;	LD	A,09H
;	CALL	DOS02
;	CALL	DOS02
M10:
	LD	IX,L.DATA
	LD	A,(IX+0)
	CP	09H
	JP	Z,MAIN10
	CP	' '
	JP	Z,MAIN10
	OR	A
	JP	Z,MAIN50
	CP	';'
	JP	Z,MAIN50
;	CP	'#'
;	JR	NZ,M11
;	JP	SUBCOM

;	LD	A,(IX+1)
;	CP	' '
;	JP	Z,SUBCOM

M11:	CALL	UPPER
	CP	' '+1
	JP	C,ERR4	;label error
	CP	':'
	JP	Z,ERR4
;	CP	'Z'+1
;	JP	NC,ERR4
	LD	HL,WORK1
	XOR	A
	LD	(HL),A
	INC	HL
	LD	B,40+2
	LD	A,' '
M10.1:	LD	(HL),A
	INC	HL
	DJNZ	M10.1
	LD	DE,WORK1+1
	LD	B,40
LBLS10:	LD	A,(IX+0)
	CALL	UPPER
	CP	':'
	JP	Z,LBLS17
	CP	09H
	JP	Z,L.EQU
	CP	' '
	JP	Z,L.EQU
;	CP	'_'+1
;	JP	NC,ERR4
	CP	';'
	JP	Z,ERR4
	LD	(DE),A
	INC	IX
	INC	DE
	DJNZ	LBLS10
LBLS15:	LD	A,(IX+0)
	CALL	UPPER
	CP	':'
	JP	Z,LBLS17
	CP	09H
	JP	Z,L.EQU
	CP	' '
	JP	Z,L.EQU
	CP	' '+1
	JP	C,ERR4
;	CP	'_'+1
;	JP	NC,ERR4
	CP	';'
	JP	Z,ERR4
	INC	IX
	JP	LBLS15

LBLS17:	LD	A,40
	SUB	B
	LD	(WORK1),A
	JP	LBLS20

;--------

L.EQU:	LD	A,40
	SUB	B
	LD	(WORK1),A
	CALL	S.SKIP
	JP	Z,ERR6
	LD	C,'E'
	CALL	L.EQU2
	LD	C,'Q'
	CALL	L.EQU2
	LD	C,'U'
	CALL	L.EQU2

;	LD	C,' '
;	CALL	L.EQU2
	LD	A,(IX+0)
	INC	IX
	CP	09H
	JR	Z,L.EQU1
	CP	' '
	JR	Z,L.EQU1
	JP	SUBCOM
L.EQU1:
	CALL	S.SKP2
	JP	Z,ERR6
	PUSH	DE
	CALL	GET.NO
	POP	DE
;	JP	C,ERR6
	DEC	IX
	CALL	S.SKIP
	JP	NZ,ERR6
	DEC	IX
	LD	HL,(VAL1)
	EX	DE,HL
	LD	(HL),E
	INC	HL
	LD	(HL),D
	JP	LBLS25

L.EQU2:	LD	A,(IX+0)
	INC	IX
	CALL	UPPER
	CP	C
	RET	Z
	POP	HL	;dummy
	JP	SUBCOM	;JP	ERR6

LBLS20:
	LD	HL,(O.ADR1)
	EX	DE,HL
	LD	(HL),E
	INC	HL
	LD	(HL),D
LBLS25:	LD	HL,WORK1
	LD	(PNT2),HL
	CALL	SRCHL	;label search
	JP	C,LBLS32

	LD	A,(PASS)
	DEC	A
	JP	Z,ERR5	;Multiply defined label
	OR	A

LBLS30:	LD	HL,(PNT2)
	PUSH	AF
	JR	LBLS35
LBLS32:	PUSH	AF
	LD	HL,(LBL.NX)
LBLS35:	EX	DE,HL
;	PUSH	DE

	CALL	SET.LBL

;	POP	DE

	POP	AF
	JR	NC,LBLS36
	LD	(LBL.NX),DE
	LD	HL,(LBL.NO)
	INC	HL
	LD	(LBL.NO),HL
	LD	HL,(LBL.NX)
	LD	DE,0D000H
	OR	A
	SBC	HL,DE
	JP	NC,ERR8	;label table full
LBLS36:
;-----------------------

MAIN10:	LD	HL,WORK2
	LD	A,' '
	LD	(HL),A
	INC	HL
	LD	(HL),A
	INC	HL
	LD	(HL),A
	INC	HL
	LD	(HL),A

	INC	HL
	LD	(HL),A
	INC	HL
	LD	(HL),A

	CALL	S.SKIP
	JP	Z,MAIN50
	LD	DE,WORK2
	LD	B,7	;5
MAIN11:	LD	A,(IX+0)
	CALL	UPPER
	CP	'A'
	JP	NC,MAIN12
	OR	A
	JR	Z,MAIN15
	CP	09H
	JR	Z,MAIN15
	CP	' '
	JR	Z,MAIN15
	CP	';'
	JR	Z,MAIN15
	JP	ERR6
MAIN12:	CP	'Z'+1
	JP	NC,ERR6
	LD	(DE),A
	INC	IX
	INC	DE
	DJNZ	MAIN11
	JP	ERR6
MAIN15:	LD	A,7	;5
	SUB	B
	LD	(LEN),A
;	LD	(PNT),IX
	LD	HL,WORK2
	LD	A,(HL)
	SUB	'A'
	LD	B,A
	ADD	A,A
	ADD	A,B
	LD	H,0
	LD	L,A
	LD	DE,OP.TBL
	ADD	HL,DE
	LD	A,(HL)
	LD	B,A
	OR	A
	JP	Z,ERR6
	INC	HL
	LD	A,(HL)
	INC	HL
	LD	H,(HL)
	LD	L,A
OPE03:	PUSH	BC
	LD	DE,WORK2+1	;(PNT2)
	LD	B,5	;3
OPE05:	LD	A,(DE)
	CP	(HL)
	JP	NZ,OPE10
	INC	HL
	INC	DE
	DJNZ	OPE05
OPE050:	POP	BC
;86	LD	A,(HL)
;	CP	40H
;	JR	NC,OPE15
OPE06:
	LD	E,(HL)
	INC	HL
	LD	D,(HL)
	EX	DE,HL
	JP	(HL)

;86	LD	DE,J.TBL1
;	ADD	A,A
;	LD	H,0
;	LD	L,A
;	ADD	HL,DE
;	LD	A,(HL)
;	INC	HL
;	LD	H,(HL)
;	LD	L,A
;	JP	(HL)

OPE10:	INC	HL
	INC	HL	;86
OPE11:	INC	HL
	DJNZ	OPE11
	POP	BC
	DJNZ	OPE03
	JP	ERR6

;
;
;
SUBCOM:	LD	IX,L.DATA
 	LD	A,(IX+0)
	CP	'#'
	JP	NZ,ERR6
	INC	IX
	LD	DE,SUBD1
	LD	C,1
SUB1:	PUSH	IX
SUB2:	LD	A,(DE)
	OR	A
	JR	Z,SUB5
	LD	B,A
	LD	A,(IX+0)
	CALL	UPPER
	CP	B
	JR	NZ,SUB3
	INC	IX
	INC	DE
	JR	SUB2
SUB3:	INC	C
	POP	IX
SUB4:	LD	A,(DE)
	INC	DE
	CP	0FFH
	JP	Z,SUBE
	OR	A
	JR	NZ,SUB4
	JR	SUB1

SUB5:	POP	AF
	DEC	C
	JR	Z,SUBI
	DEC	C
	JP	Z,SUBIF
	DEC	C
	JP	Z,SUBM
SUBE:	LD	DE,SUBEM1
	CALL	DOS09
	JP	ERR6

SUBEM1:	DB	'subcom error!$'

SUBD1:	DB	'INCLUDE',0
	DB	'IF',0
	DB	'MACRO',0
	DB	0FFH

SUBI:	LD	DE,SUBIM1
	CALL	DOS09
	LD	HL,SUBWRK
	LD	B,11
SUBI0:	LD	(HL),20H
	INC	HL
	DJNZ	SUBI0
	CALL	S.SKIP
	JP	Z,ERR6
	CP	'<'
	JP	NZ,ERR6
	CALL	S.SKIP
	JP	Z,ERR6
	LD	DE,SUBWRK
SUBI1:	LD	A,(IX+0)
	INC	IX
	CP	'.'
	JR	Z,SUBI5
	CP	'>'
	JR	Z,SUBI7
	CALL	DOS02
	LD	(DE),A
	INC	DE
	JR	SUBI1
SUBI5:	LD	DE,SUBWRK+8
	LD	A,'.'
	CALL	DOS02
SUBI6:	LD	A,(IX+0)
	INC	IX
	CP	'>'
	JR	Z,SUBI7
	CALL	DOS02
	LD	(DE),A
	INC	DE
	JR	SUBI6
SUBI7:
	CALL	CRLF
	LD	A,(LOAD.F)
	INC	A
	LD	(LOAD.F),A
	CP	16
	JP	Z,ERRC	;too many include file

	PUSH	IX

	LD	IX,(FCB)

	LD	HL,(LIN.NO)
	INC	HL
	LD	(IX+25H),L
	LD	(IX+26H),H

	LD	L,(IX+33)
	LD	H,(IX+34)
	LD	DE,(S.LEFT)
	OR	A
	SBC	HL,DE
	LD	(IX+33),L
	LD	(IX+34),H
	LD	L,(IX+35)
	LD	H,(IX+36)
	LD	DE,0
	SBC	HL,DE
	LD	(IX+35),L
	LD	(IX+36),H

	LD	HL,(FCB)
	LD	DE,25H+2
	ADD	HL,DE
	LD	(FCB),HL
	LD	(HL),0
	PUSH	HL
	EX	DE,HL
	INC	DE
	LD	HL,SUBWRK
	LD	BC,11
	LDIR
	XOR	A
	LD	(FL.END),A
	LD	HL,0
	LD	(S.LEFT),HL
	LD	DE,(FCB)
	CALL	DOS0F
	POP	IX
	LD	(IX+14),1
	LD	(IX+15),0
	LD	(IX+33),0
	LD	(IX+34),0
	LD	(IX+35),0
	LD	(IX+36),0

	POP	IX

	JP	NZ,ERR0
	LD	HL,0
	LD	(LIN.NO),HL

	JP	MAIN20
;
;
;
SUBM:	CALL	S.SKIP
SUBM10:	LD	A,(IX+0)
	OR	A
	JP	Z,ERR6
	CP	';'
	JP	Z,ERR6

SUBM11:	CALL	UPPER
	CP	' '+1
	JP	C,ERR4	;label error
	LD	HL,WORK1
	XOR	A
	LD	(HL),A
	INC	HL
	LD	B,40+2
	LD	A,' '
SUBM12:	LD	(HL),A
	INC	HL
	DJNZ	SUBM12
	LD	DE,WORK1+1
	LD	B,40
SUBM15:	LD	A,(IX+0)
	CALL	UPPER

	LD	(DE),A
	INC	IX
	INC	DE
	DJNZ	SUBM15
SUBM17:	LD	A,(IX+0)
	CALL	UPPER
	CP	':'
	JP	Z,ERR6
	CP	09H
	JP	Z,SUBM18
	CP	' '
	JP	Z,SUBM18
	CP	' '+1
	JP	C,ERR4
	CP	';'
	JP	Z,SUBM18
	INC	IX
	JP	SUBM17

SUBM18:	LD	A,40
	SUB	B
	LD	(WORK1),A

SUBM20:

SUBIM1:	DB	'LOADING...$'


SUBIF:	CALL	OPCODE
	CP	50H
	JP	NZ,ERRB
	LD	A,H
	OR	L
	LD	(IF.FLG),A
	JP	MAIN20

SUBENDIF:
	XOR	A
	LD	(IF.FLG),A
	DEC	IX
	CALL	S.SKIP
	JP	NZ,ERRB
	JP	MAIN20

IF.FLG:	DS	1

SUBWRK:	DS	16


#INCLUDE <ASM8602.ASM>
#INCLUDE <ASM8603.ASM>
#INCLUDE <ASM8604.ASM>
